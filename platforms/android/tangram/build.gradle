apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

// -----------------------------------------------------------------------------
//  Version detection from git tag
// -----------------------------------------------------------------------------
def getGitTag() {
    // Try exact tag on HEAD first
    try {
        def exact = ['git', 'describe', '--tags', '--exact-match'].execute(null, rootDir)
        exact.waitFor()
        if (exact.exitValue() == 0) {
            return exact.in.text.trim()
        }
        // Fallback to nearest tag (most recent)
        def nearest = ['git', 'describe', '--tags', '--abbrev=0'].execute(null, rootDir)
        nearest.waitFor()
        if (nearest.exitValue() == 0) {
            return nearest.in.text.trim()
        }
    } catch (Exception ignored) {
        // If git isn't available for some reason, ignore and fallback below
    }
    return null
}

def rawTag = System.getenv('JITPACK_TAG') ?: getGitTag() // keep env var option if present
def normalizedVersion = null
if (rawTag) {
    // strip common 'v' prefix: v1.2.3 -> 1.2.3
    normalizedVersion = rawTag.replaceFirst(/^v/, '')
}
// Fallback version for branch/commit builds
if (!normalizedVersion) {
    // you can choose commit hash or snapshot form
    def commit = System.getenv('JITPACK_COMMIT')
    normalizedVersion = commit ? "0.0.0-${commit.substring(0,7)}" : '0.0.0-SNAPSHOT'
}

project.version = normalizedVersion
project.group = 'com.styluslabs.tangram'

println "Project version set to ${project.version}"

android {
  compileSdkVersion 36
  namespace project.group
  buildToolsVersion = System.getenv('BUILD_TOOLS') ?: '35.0.0'

  // Specific ABI targets can be chosen with the 'abis' property as a comma-separated list of ABIs.
  // From the command line, add '-Ptangram.abis=armeabi-v7a,x86,...' to only build those ABIs.
  // When building from Android Studio, the ABI for the target device is injected and takes precedence.
  def abi = 'arm64-v8a'
  if (!project.hasProperty('android.injected.invoked.from.ide') && project.hasProperty('tangram.abis')) {
    abi = project.getProperty('tangram.abis')
  }

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 36

    // Keep versionName in sync with project.version
    versionName project.version

    consumerProguardFiles 'tangram-proguard-rules.txt'
    buildConfigField 'String', 'TAG', '"Tangram"'
    externalNativeBuild {
      cmake {
        targets 'tangram'
        arguments '-DTANGRAM_PLATFORM=android',
                  '-DANDROID_STL=c++_shared'
                  '-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON'
        cppFlags '-std=c++20',
                 '-fvisibility=hidden', // export only JNIEXPORT
                 '-pedantic',
                 '-fPIC',
                 '-fexceptions',
                 '-frtti',
                 // linker
                 '-Wl,--exclude-libs,ALL', // dont export static lib symbols
                 //warnings
                 '-Wall',
                 '-Wignored-qualifiers',
                 '-Wtype-limits',
                 '-Wno-missing-field-initializers',
                 '-Wno-format-pedantic',
                 '-Wno-gnu-statement-expression',
                 '-Wno-gnu-anonymous-struct',
                 '-Wno-nested-anon-types',
                 '-Wno-unused-command-line-argument', // for -Wl linker flags..
                 '-Wno-unknown-warning-option'
        if (project.findProperty('tangram.ccache')) {
          arguments += '-DCMAKE_CXX_COMPILER_LAUNCHER=ccache'
          arguments += '-DCMAKE_C_COMPILER_LAUNCHER=ccache'
        }
        if (abi == 'all') {
          abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        } else {
          abiFilters abi.split(',')
        }

      }
    }
  }

  externalNativeBuild {
    cmake {
      version System.getenv('CMAKE_VERSION') ?: '3.30.5'
      path '../../../CMakeLists.txt'
    }
  }

  // next time, be sure to install versions listed at developer.android.com/build/releases/gradle-plugin#compatibility
  ndkVersion System.getenv('NDK_VERSION') ?: '28.2.13676358'

  buildFeatures {
    buildConfig true
  }

  lintOptions {
    abortOnError false
    disable 'ExpiredTargetSdkVersion'
  }

  buildTypes {
    debug {
      externalNativeBuild {
        cmake.cppFlags '-g'
      }
    }
    release {
      externalNativeBuild {
        cmake.cppFlags'-g0'
      }
    }
  }
}

dependencies {
  api 'com.squareup.okhttp3:okhttp:5.2.1'
  implementation 'androidx.annotation:annotation:1.1.0'
}


// -----------------------------------------------------------------------------
//  Maven publish using project.version (so JitPack's tag is used)
// -----------------------------------------------------------------------------
afterEvaluate {
    publishing {
        publications {
            // Name the publication (e.g. 'aar'); ensure it matches 'components.release' if you publish release AAR
            aar(MavenPublication) {
                from components.findByName('release')
                groupId = project.group
                artifactId = 'tangram-ng-android'
                version = project.version.toString()
            }
        }
    }
}
